plugins {
    id 'java'
    id 'jacoco'
    id 'application'
    id 'info.solidsoft.pitest' version '1.15.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework:spring-core:6.0.12'
    implementation 'org.springframework:spring-context:6.0.12'

    implementation("com.squareup.okhttp3:okhttp:5.0.0-alpha.11")
    implementation("org.apache.httpcomponents:httpclient:4.5.14")
    implementation("com.fasterxml.jackson.core:jackson-databind:2.15.2")


    testImplementation(platform("org.junit:junit-bom:5.10.0"))
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation('org.assertj:assertj-core:3.24.2')
    testImplementation("com.approvaltests:approvaltests:21.0.0")
    testImplementation("com.larseckart:junit-tcr-extensions:0.0.3")
    testImplementation("org.mockito:mockito-core:5.5.0")
    testImplementation("org.mockito:mockito-junit-jupiter:5.5.0")
    testImplementation("com.squareup.okhttp3:mockwebserver3:5.0.0-alpha.11")
    testImplementation("com.squareup.okhttp3:mockwebserver3-junit5:5.0.0-alpha.11")
}

tasks.withType(JavaCompile) {
    options.compilerArgs.add("-Xlint:all")
    options.incremental = true
}


application {
    mainClass.set("kata.App")
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes "Main-Class": "kata.App"
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

project.tasks.check.dependsOn("pitest")
pitest {
    junit5PluginVersion = '1.0.0'
    testSourceSets = [sourceSets.test]
    mainSourceSets = [sourceSets.main]
    targetClasses = ['kata.*']
    pitestVersion = '1.9.11'
    threads = 4
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
}
